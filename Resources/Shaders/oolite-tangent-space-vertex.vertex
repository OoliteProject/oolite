/*
	oolite-tangent-space-vertex.vertex
	Basic vertex shader for Oolite ships using tangent-space effects.
	
	
	© 2008 Jens Ayton

	This program is free software; you can redistribute it and/or
	modify it under the terms of the GNU General Public License
	as published by the Free Software Foundation; either version 2
	of the License, or (at your option) any later version.

	This program is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU General Public License for more details.

	You should have received a copy of the GNU General Public License
	along with this program; if not, write to the Free Software
	Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
	MA 02110-1301, USA.
	
	
	This file may also be distributed under the MIT/X11 license:
	
	Copyright © 2008 Jens Ayton
	
	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:
	
	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.
	
	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
	SOFTWARE.
*/

attribute vec3			tangent;


#ifndef OO_LIGHT_0_FIX
#define OO_LIGHT_0_FIX 0
#endif


// No vNormal, because normal is always 0,0,1 in tangent space.
varying vec3			vEyeVector;
varying vec2			vTexCoord;
#if OO_LIGHT_0_FIX
varying vec3			vLight0Vector;
#endif
varying vec3			vLight1Vector;

#ifndef OOSTD_DIFFUSE_MAP_IS_CUBE_MAP
#define OOSTD_DIFFUSE_MAP_IS_CUBE_MAP 0
#endif

#if OOSTD_DIFFUSE_MAP_IS_CUBE_MAP
varying vec3			vRawNormal;
#endif


void main(void)
{
	// Build tangent basis
	vec3 n = normalize(gl_NormalMatrix * gl_Normal);
	vec3 t = normalize(gl_NormalMatrix * tangent);
	vec3 b = cross(n, t);
	
	mat3 TBN = mat3(t, b, n);
	
	vec3 eyeVector = -vec3(gl_ModelViewMatrix * gl_Vertex);
	vEyeVector = eyeVector * TBN;
	
#if OO_LIGHT_0_FIX
	vec3 light0Vector = gl_LightSource[0].position.xyz + eyeVector;
	vLight0Vector = light0Vector * TBN;
#endif
	
	vec3 light1Vector = gl_LightSource[1].position.xyz + eyeVector;
	vLight1Vector = light1Vector * TBN;
	
	vTexCoord = gl_MultiTexCoord0.st;
	
#if OOSTD_DIFFUSE_MAP_IS_CUBE_MAP
	vRawNormal = gl_Normal;
#endif
	
	gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
}
